plugins {
    id 'java'
    id 'java-library'
}

group = 'net.rms.xrain.velocityass'
version = file('plugin.version').text.trim().substring(1)  // 去除V前缀
sourceCompatibility = '17'

repositories {
    // 本地API jar文件目录
    flatDir {
        dirs 'api'
    }

    // 华为云Maven镜像优先
    maven {
        name = 'huawei-maven'  
        url = 'https://repo.huaweicloud.com/repository/maven/'
    }
    
    // PaperMC仓库
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    // 使用本地构建的velocity-api jar文件
    compileOnly files('api/velocity-api-3.1.1-SNAPSHOT-all.jar')
    annotationProcessor files('api/velocity-api-3.1.1-SNAPSHOT-all.jar')
    
    implementation 'org.yaml:snakeyaml:2.0'
    
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

compileJava {
    options.encoding = 'UTF-8'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'net.rms.xrain.velocityass.AutoSelectServer'
    }
}

// 更新velocity-plugin.json中的版本号
task updatePluginJson {
    doLast {
        def pluginJsonFile = file('src/main/resources/velocity-plugin.json')
        def pluginJson = new groovy.json.JsonSlurper().parse(pluginJsonFile)
        pluginJson.version = version
        
        def jsonOutput = new groovy.json.JsonBuilder(pluginJson)
        pluginJsonFile.text = groovy.json.JsonOutput.prettyPrint(jsonOutput.toString())
    }
}

// 更新Java源文件中@Plugin注解的版本号
task updateJavaVersion {
    doLast {
        def javaFile = file('src/main/java/net/rms/xrain/velocityass/AutoSelectServer.java')
        def content = javaFile.text
        content = content.replaceAll(/version = "[\d\.]+",/, "version = \"${version}\",")
        javaFile.text = content
    }
}

// 确保在编译之前更新版本号
compileJava.dependsOn updatePluginJson, updateJavaVersion